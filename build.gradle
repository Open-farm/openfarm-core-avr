apply from: 'util.gradle'
apply from: 'properties.gradle'

// This task generates documentation.
task gendocs(dependsOn:'build') {
	doLast {
		exec_cmd([
			'doxygen',
			'.doxygen.config'])
	}
}

// This task build libs to static libraries for avr architecture.
task buildavr {
	doLast { task ->
		def libs = new File('libs')
		if (!libs.exists())
		{
			libs.mkdir()
		}

		def thirdPartyDir = new File('thirdparty')
		def baseCoreDir = new File('.')

		exec_cmd([
			'make',
			"OUT_DIR=$outDir",
			"OBJ_DIR=$objDir",
			"ARDUINODIR=$arduinoDir",
			"BASECOREDIR=${baseCoreDir.getAbsolutePath()}",
			"THIRDPARTYDIR=${thirdPartyDir.getAbsolutePath()}",

			"CC=$cc",
			"ARCHIVER=$archiver",
			"CFLAGS=-c -g -Os -Wall -Werror -std=gnu++11 -fno-exceptions -ffunction-sections -fdata-sections -fno-threadsafe-statics -MMD -DARDUINO=100 -DARDUINO_AVR_NANO -DARDUINO_ARCH_AVR -mmcu=$cpuName -DF_CPU=$cpuClock",
			"COREINC=-I$coreInc",
			"VARIANTINC=-I$arduinoVariant",
			"ARDUINOINC=-I$arduinoInc"])
	}
}

// This task builds tests separately than libs.
task buildtests {
	doLast {
		def thirdPartyDir = new File('thirdparty')
		def baseCoreDir = new File('.')

		exec_cmd([
			'make',
			'buildtests',
			"OUT_DIR=$outDir",
			"OBJ_DIR=$objDir",
			"BASECOREDIR=${baseCoreDir.getAbsolutePath()}",
			"THIRDPARTYDIR=${thirdPartyDir.getAbsolutePath()}",
			"CC=g++",
			"TESTINC=-I$testInc"])
	}
}

// Cleans lib + generated folders.
task clean {
	doLast {
		exec_cmd(['rm', '-rf', 'libs'])
		exec_cmd(['rm', '-rf', 'docs/generated/html'])
	}
}

// Generates a project setup for testing.
task gentests {
	doLast { task ->
		if (!task.project.hasProperty('api')) {
			println('No tests generated. Must be supplied with -Papi property.');
			return;
		}

		def api = task.project.api;
		def apiDir = new File(api);
		if (!apiDir.exists()) {
			apiDir.mkdir();
		}

		def testDir = new File(apiDir, 'test');
		if (!testDir.exists()) {
			testDir.mkdir();
		}

		// copy makefile
		def src = new File('templates/tests/makefile.example')
		def dst = new File("$api/test/makefile")
		dst << src.text

		// prep test main
		dst = new File("$api/test/Main_${api}.cpp")
		if (!dst.createNewFile()) {
			println("Could not create ${dst.getAbsolutePath()}.")
			return;
		}
		dst << "\
#define CATCH_CONFIG_MAIN\n\
#include <catch.hpp>"

		println("Tests generated in ${api}/tests.");
	}
}